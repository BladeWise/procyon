import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

ext.getProcyonVersion = { ->
    final def fallbackVersion = "1.0-SNAPSHOT"
    final def versionFilePath = rootDir.canonicalPath + "/Procyon.Core/src/main/java/com/strobel/Procyon.java"
    final def versionFile = new File(versionFilePath)

    if (versionFile.exists()) {
        try {
            final String fileContents = new File(versionFilePath).getText('UTF-8')
            final def matcher = fileContents =~ /VERSION\s*=\s*"([^"]+)"/

            if (matcher.find()) {
                return matcher.group(1).trim()
            }
        }
        catch (final Throwable ignored) {
        }
    }

    logger.warn("WARNING: Could not resolve version from source; falling back to '$fallbackVersion'.")

    return fallbackVersion
}

final def procyonVersion = getProcyonVersion()

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    buildDir = rootDir.canonicalPath + "/build/" + rootProject.relativePath(projectDir.canonicalPath)

    sourceCompatibility = 1.7    // JDK version
    targetCompatibility = 1.7

    group 'com.github.mstrobel'
    version procyonVersion

    ext {
        vendor = 'Mike Strobel'
        title = null
    }

    final boolean shouldSign = "true".equalsIgnoreCase(System.properties.getProperty("procyon.signing.enabled"))

    archivesBaseName = "procyon-${(it.name as String).split('\\.')[1].toLowerCase()}"

    jar {
        metaInf {
            from 'License.txt'
            from 'README.md'
        }
        manifest {
            attributes 'Implementation-Vendor': "${-> project.vendor}",
                    'Implementation-Version': archiveVersion,
                    'Implementation-Title': "${-> project.title}"
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
    }

    test {
        useJUnit()
    }

    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED
            exceptionFormat TestExceptionFormat.FULL
            showExceptions true
            showCauses true
            showStackTraces true

            // set options for log level DEBUG and INFO
            debug.with {
                events TestLogEvent.STARTED,
                        TestLogEvent.FAILED,
                        TestLogEvent.PASSED,
                        TestLogEvent.SKIPPED,
                        TestLogEvent.STANDARD_ERROR,
                        TestLogEvent.STANDARD_OUT
                exceptionFormat TestExceptionFormat.FULL
            }

            info.events = debug.events
            info.exceptionFormat = debug.exceptionFormat

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length() - 2
                    logger.lifecycle('\n' + '+' + ('-' * repeatLength) + '+' + '\n' + startItem + output + endItem + '\n' + '+' + ('-' * repeatLength) + '+')
                }
            }
        }
    }

    if (project.name == "Procyon.Decompiler") {
        artifacts {
            archives jar
        }
    } else {
        javadoc {
            options.encoding = 'UTF-8'
        }

        if (shouldSign) {
            signing {
                sign publishing.publications.mavenJava
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = project.archivesBaseName
                    description = 'Procyon'
                    url = 'https://github.com/mstrobel/procyon'

                    scm {
                        url = 'https://github.com/mstrobel/procyon'
                        connection = 'scm:git:https://github.com/mstrobel/procyon.git'
                        developerConnection = 'scm:git:https://mstrobel@github.com/mstrobel/procyon.git'
                    }

                    issueManagement {
                        system = 'github'
                        url = 'https://github.com/mstrobel/procyon/issues'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'mstrobel'
                            name = 'Mike Strobel'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username project.properties.get("procyon.sonatype.username")
                    password project.properties.get("procyon.sonatype.password")
                }
            }
        }
    }
}
